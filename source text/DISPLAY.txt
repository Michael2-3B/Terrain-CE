ClrDraw
2->R
16->N
BackgroundOn DARKGRAY

//Draw MiniMap
RED
Line(3,~3,3,~52,Ans,1
Line(3,~3,52,~3,Ans,1
Line(3,~52,52,~52,Ans,1
Line(52,~3,52,~52,Ans,1
For(B,0,N-1
	For(A,0,N-1
		[A](A+1,B+1
		Ans-13(Ans=25)-2(Ans=20
		If Ans>25:RED
		Pt-On(3A+5,~3B-5,1,Ans
	End
End

//Draw Isometric Terrain
For(A,1,BROWN //substituting values in For loops with colors
	~80+20(R-2)-R(A-1->S
	132-6A->T
	For(B,1,BROWN
		[A](B,A)-19->V
		BROWN+2(Ans<2)+3(Ans=2->I%
		BLACK-2(Ans=LTBLUE->O
		If V>2
		I%+6(rand>.5->I%
		T+6B->|N
		S-BR-R+6(V-1->Z
		S-BR+6V-R
		
		//Fill Box
		For(I,Z,Ans,2
			Line(|N-5,I,|N+5,I,I%
		End
		Pt-Off(|N-5,Z-R+1,4
		Pt-Off(|N+5,Z-R+1,4
		Line(|N-R,Ans+R-1,|N+R,Ans+R-1,I%,1

		//Draw Grass
		If V>2 and I%!=MEDGRAY
		Then
			For(I,0,2
				Line(|N-5+2I,Ans-I,|N+2I,Ans+1,GREEN
			End
			Line(|N,Ans-3,|N+6,Ans+R-3,GREEN,1
		End
		
		//Draw Main Vertical Edge
		Line(|N,Z-R,|N,Ans-R,O,1

		//Other Edges
		For(I,~6,6,Black
			Line(|N+I,Z,|N,Z-R,O,1
			Line(|N+I,Ans,|N+I,Z,O,1
			Line(|N,Ans+R,|N+I,Ans,O,1
			Line(|N+I,Ans,|N,Ans-R,O,1
		End
	End
End